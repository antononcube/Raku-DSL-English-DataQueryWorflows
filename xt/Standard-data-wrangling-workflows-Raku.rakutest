use v6.d;

# The DSL specs of tests correspond to the examples in the document:
# ../doc/Standard-data-wrangling-commands.md
#
# The code Raku code for the $rex?Expected variables was generated using
# the CLI script ToDataQueryWorkflowCode (provided by this package.)
#
# In some cases the expected code can be made to be "just" with core Raku functions.
# (I.e. without functions from Data::Reshapers.)

use Stats;
use Data::Reshapers;
use Data::Summarizers;
use Data::ExampleDatasets;
use DSL::English::DataQueryWorkflows;

# From Data::Reshapers
my @dfTitanic = get-titanic-dataset();

# From MathematicaVsR at GitHub using Data::ExampleDatasets
my @dfStarwars = example-dataset("https://raw.githubusercontent.com/antononcube/R-packages/master/DataQueryWorkflowsTests/inst/extdata/dfStarwars.csv");
my @dfStarwarsFilms = example-dataset("https://raw.githubusercontent.com/antononcube/R-packages/master/DataQueryWorkflowsTests/inst/extdata/dfStarwarsFilms.csv");
#my @dfStarwarsStarships = example-dataset("https://raw.githubusercontent.com/antononcube/R-packages/master/DataQueryWorkflowsTests/inst/extdata/dfStarwarsStarships.csv");
#my @dfStarwarsVehicles = example-dataset("https://raw.githubusercontent.com/antononcube/R-packages/master/DataQueryWorkflowsTests/inst/extdata/dfStarwarsVehicles.csv");

# Main use case for Data::ExampleDatasets
my @dfAnscombe = example-dataset('anscombe');

use MONKEY-SEE-NO-EVAL;
use Test;

plan 14;

##===========================================================
## 1 Trivial workflow
##===========================================================
my $command1 = "use @dfTitanic; group by passengerSex; counts; take value";

ok ToDataQueryWorkflowCode( $command1, target => 'Raku::Reshapers');

my $code1 = ToDataQueryWorkflowCode( $command1, target => 'Raku::Reshapers');

# We have declare $obj, because the ToDataQueryWorkflowCode does define it.
my $obj;

# Evaluate the generated code.
my $res1 = EVAL($code1);

my $res1Expected;
{
    my $obj = @dfTitanic.clone;
    $obj = group-by($obj, 'passengerSex');
    $obj = $obj>>.elems;
    $res1Expected = $obj;
}

is-deeply $res1, $res1Expected, 'trivial workflow';

##===========================================================
## 2 Cross tabulation
##===========================================================

my $command2 = "
use @dfTitanic;
filter with passengerSex is 'male' and passengerSurvival equals 'died' or passengerSurvival is 'survived' ;
cross tabulate passengerClass, passengerSurvival over passengerAge;
take value";

ok ToDataQueryWorkflowCode( $command2, target => 'Raku::Reshapers');

my $code2 = ToDataQueryWorkflowCode( $command2, target => 'Raku::Reshapers');

# Evaluate the generated code.
my $res2 = EVAL($code2);

my $res2Expected;
{
    my $obj = @dfTitanic ;
    $obj = $obj.grep({ $_{"passengerSex"} eq "male" and $_{"passengerSurvival"} eq "died" or $_{"passengerSurvival"} eq "survived" }).Array ;
    $obj = cross-tabulate( $obj, "passengerClass", "passengerSurvival", "passengerAge" ) ;
    $res2Expected = $obj;
}

is-deeply $res2, $res2Expected, 'cross tabulation';


#===========================================================
## 3 Mutation with formulas
##===========================================================

my $command3 = '
use data frame @dfStarwars;
keep the columns name, homeworld, mass & height;
replace missing with `<0>`;
transform with bmi = `$_<mass>/$_<height>**2*10000` and homeworld = `$_<homeworld>.uc`;
filter rows by bmi >= 30 & height < 200;
arrange by the variables mass & height descending';

ok ToDataQueryWorkflowCode( $command3, target => 'Raku::Reshapers');

my $code3 = ToDataQueryWorkflowCode( $command3, target => 'Raku::Reshapers');

# Evaluate the generated code.
my $res3 = EVAL($code3);

my $res3Expected;
{
    $obj = @dfStarwars ;
    $obj = select-columns($obj, ("name", "homeworld", "mass", "height") ) ;
    $obj = $obj.deepmap({ ( ($_ eqv Any) or $_.isa(Nil) or $_.isa(Whatever) ) ?? <0> !! $_ }) ;
    $obj = $obj.map({ $_{"bmi"} = $_<mass>/$_<height>**2*10000; $_{"homeworld"} = $_<homeworld>.uc; $_ }) ;
    $obj = $obj.grep({ $_{"bmi"} >= 30 and $_{"height"} < 200 }).Array ;
    $obj = $obj.sort({($_{"mass"}, $_{"height"}) }).reverse.Array;
    $res3Expected = $obj;
}

#.say for |$res3;
#say '-' x 120;
#.say for |$res3Expected;

is-deeply $res3, $res3Expected, 'mutation with formulas';

##===========================================================
## 4 Grouping awareness
##===========================================================

my $command4 = "use @dfTitanic;
group by passengerClass;
group by passengerSex;
counts;
take value";

ok ToDataQueryWorkflowCode( $command4, target => 'Raku::Reshapers');

my $code4 = ToDataQueryWorkflowCode( $command4, target => 'Raku::Reshapers');

# Evaluate the generated code.
my $res4 = EVAL($code4);

my $res4Expected;
{
    $obj = @dfTitanic ;
    $obj = group-by($obj, "passengerClass") ;
    $obj = group-by($obj.values.reduce( -> $x, $y { [|$x, |$y] } ), "passengerSex") ;
    $obj = $obj>>.elems;
    $res4Expected = $obj;
}

is-deeply $res4, $res4Expected, 'grouping awareness';


##===========================================================
## 5 Non-trivial workflow
##===========================================================

my $command5 = '
use @dfStarwars;
replace missing with `<NaN>`;
mutate with mass = `+$_<mass>` and height = `+$_<height>`;
filter by birth_year greater than 27;
select homeworld, mass and height;
group by homeworld;
summarize the variables mass and height with &mean and &median
';

ok ToDataQueryWorkflowCode( $command5, target => 'Raku::Reshapers');

my $code5 = ToDataQueryWorkflowCode( $command5, target => 'Raku::Reshapers');

# Evaluate the generated code.
my $res5 = EVAL($code5);

my $res5Expected;
{
    $obj = @dfStarwars ;
    $obj = $obj.deepmap({ ( ($_ eqv Any) or $_.isa(Nil) or $_.isa(Whatever) ) ?? <NaN> !! $_ }) ;
    $obj = $obj.map({ $_{"mass"} = +$_<mass>; $_{"height"} = +$_<height>; $_ }) ;
    $obj = $obj.grep({ $_{"birth_year"} > 27 }).Array ;
    $obj = select-columns($obj, ("homeworld", "mass", "height") ) ;
    $obj = group-by($obj, "homeworld") ;
    $obj = $obj.map({ $_.key => summarize-at($_.value, ("mass", "height"), (&mean, &median)) });

    $res5Expected = $obj;
}

is-deeply $res5.Hash, $res5Expected.Hash, 'non-trivial workflow';


##===========================================================
## 6 Joins
##===========================================================

my $command6 =  "use @dfStarwarsFilms;
left join with @dfStarwars by 'name';
replace missing with `<NaN>`;
sort by name, film desc;
take pipeline value";

ok ToDataQueryWorkflowCode( $command6, target => 'Raku::Reshapers');

my $code6 = ToDataQueryWorkflowCode( $command6, target => 'Raku::Reshapers');

# Evaluate the generated code.
my $res6 = EVAL($code6);

my $res6Expected;
{
    $obj = @dfStarwarsFilms ;
    $obj = join-across( $obj, @dfStarwars, ("name"), join-spec=>"Left") ;
    $obj = $obj.deepmap({ ( ($_ eqv Any) or $_.isa(Nil) or $_.isa(Whatever) ) ?? <NaN> !! $_ }) ;
    $obj = $obj.sort({($_{"name"}, $_{"film"}) }).reverse.Array ;
    $res6Expected = $obj;
}

is-deeply $res6, $res6Expected, 'joins';


##===========================================================
## 7 Complicated and neat workflow
##===========================================================

my $command7 =  '
use @dfAnscombe;
convert to long form;
separate the data column Variable into Variable and Set with separator pattern "";
to wide form for id columns Set and AutomaticKey variable column Variable and value column Value';

ok ToDataQueryWorkflowCode( $command7, target => 'Raku::Reshapers');

my $code7 = ToDataQueryWorkflowCode( $command7, target => 'Raku::Reshapers');

# This is a hack -- to-long-format has to be improved.
$code7 .= subst('@dfAnscombe', '|@dfAnscombe');

# Evaluate the generated code.
my $res7 = EVAL($code7);

my $res7Expected;
{
    $obj = |@dfAnscombe ;
    $obj = to-long-format( $obj ) ;
    $obj = separate-column( $obj, "Variable", ("Variable", "Set"), sep => "" ) ;
    $obj = to-wide-format( $obj, identifierColumns => ("Set", "AutomaticKey"), variablesFrom => "Variable", valuesFrom => "Value" );
    $res7Expected = $obj;
}

is-deeply $res7, $res7Expected, 'joins';


done-testing;
