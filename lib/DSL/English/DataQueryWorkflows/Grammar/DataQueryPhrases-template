use v6;

#use DSL::English::DataQueryWorkflows::Grammar::FuzzyMatch;
use DSL::Shared::Roles::English::CommonParts;

# Data query specific phrases
role DSL::English::DataQueryWorkflows::Grammar::DataQueryPhrases
        does DSL::Shared::Roles::English::CommonParts {
    # Tokens
    token arrange-verb { 'arrange' }
    token ascending-adjective { 'ascending' }
    token combine-verb { 'combine' }
    token cross-verb { 'cross' }
    token descending-adjective { 'descending' }
    token filter-verb { 'filter' }
    token formula-noun { 'formula' }
    token full-adjective { 'full' }
    token glimpse-verb { 'glimpse' }
    token group-verb { 'group' }
    token inner-adjective { 'inner' }
    token join-noun { 'join' }
    token left-adjective { 'left' }
    token mutate-verb { 'mutate' }
    token order-verb { 'order' }
    token rename-verb { 'rename' }
    token right-adjective { 'right' }
    token select-verb { 'select' }
    token semi-adjective { 'semi' }
    token sort-verb { 'sort' }
    token summarise-verb { 'summarise' }
    token summarize-verb { 'summarize' }
    token tabulate-verb { 'tabulate' }
    token ungroup-verb { 'ungroup' }

    rule for-which-phrase { <for-preposition> <which-determiner> | <that-pronoun> <adhere-verb> <to-preposition> }
    rule cross-tabulate-phrase { <cross-verb> <tabulate-verb> }
    rule contingency-matrix-phrase { <contingency-noun> [ <matrix-noun> | <table-noun> ] }
    rule with-formula-phrase { <with-preposition> <the-determiner>? <formula-noun> }

    # True dplyr; see comments below.
    token ascending { <ascending-adjective> | 'asc' }
    token descending { <descending-adjective> | 'desc' }
    token mutate { <mutate-verb> }
    token order { <order-verb> }

    rule arrange { [ <arrange-verb> | <order-verb> | <sort-verb> ] [ <by-preposition> | <using-preposition> ]? }
    rule data-phrase { <.the-determiner>? <data> }
    rule filter { <filter-verb> | <select-verb> }
    rule group-by { <group-verb> [ <by-preposition> | <using-preposition> ] }
    rule select { <select-verb> | 'keep' 'only'? }
    rule rename-directive { <rename-verb> }
}
